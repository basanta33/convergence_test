################################################################################
#
# RevBayes Example: Total-evidence dating under the fossilized birth-death model
#
# This file: Runs the full MCMC ...
#
# authors: Tracy A. Heath, Walker C. Pett, April M. Wright
#
################################################################################

#######################
# Reading in the Data #
#######################
# mcmc generations
gens = 200000
fromTrace = FALSE
run_PPS = FALSE

# Create the full list of taxa (including all fossils and extant species #
taxa <- readTaxonData("Data/Fossils/TotalTaxa.tsv")

# Import the molecular sequences #
# this file contains only the taxa for which sequence data are available #
filenames <- v("Data/Mol/18s.aln.nex", "Data/Mol/28s.aln.nex", "Data/Mol/ABD.aln.nex","Data/Mol/Arg.aln.nex", "Data/Mol/CAD.aln.nex", "Data/Mol/EF1.aln.nex", "Data/Mol/EF2.aln.nex", "Data/Mol/lg.aln.nex", "Data/Mol/WG.nex")
#filenames <- v("18s.nex", "28s.nex")

n_data_subsets <- filenames.size()
for (i in 1:n_data_subsets) {
   data[i] = readDiscreteCharacterData(filenames[i])
}

# Import the morphological character matrix #
# this file contains only the taxa for which morphological characters are available #
morpho <- readDiscreteCharacterData("Data/Morph/AntMegaMatrixMinusAmbig.nex")

# Add the missing taxa to each data partition #
for (i in 1:n_data_subsets) {
    data[i].addMissingTaxa( taxa )
}

morpho.addMissingTaxa( taxa )

#starting_tree <- readTrees("Data/MAP.tre")[1]

## helpers
mvi = 1
mni = 1

n_taxa <- taxa.size()
n_branches <- 2 * n_taxa - 2
# Load the model files
#source("scripts/model_FBDP_TEFBD.Rev") # FBD tree prior
#source("scripts/model_FBDP_TEFBD_skylines.Rev") # FBD tree prior
source("scripts/model_FBDP_Time_Homo.Rev") # FBD tree prior

source("scripts/model_UExp_TEFBD.Rev") # UExp relaxed clock

source("scripts/model_GTRG_TEFBD.Rev") # Molecular substitution model (GTR+G)

# source("scripts/model_Morph_TEFBD.Rev") # Morphological character change model (mk)
# source("scripts/model_MorphBlock_TEFBD.Rev") # Morphological character change model (mk with different alpha_morpho per block)
source("scripts/model_MorphBlock_TEFBD.Rev") # Morphological character change model (SHDM model)


########
# MCMC #
########

# initialize the model object #
mymodel = model(fbd_tree)
# Create a vector of monitors #
# 1. for the full model #
monitors[mni++] = mnModel(filename="output/ants_TH.log", printgen=10)
# 2. the tree #
monitors[mni++] = mnFile(filename="output/ants_TH.trees", printgen=10, fbd_tree)
# 3. and a few select parameters to be printed to the screen #
monitors[mni++] = mnScreen(printgen=10,  num_samp_anc, origin_time)
#monitors[mni++] = mnScreen(printgen=10)

# 4. the .var file for the stochastic variables (should include the tree)
monitors[mni++] = mnStochasticVariable(filename="output/ants_TH.var", printgen=10)

# 5. the stoch.log file (which may or may not be different from the .var file)
monitors[mni++] = mnStochasticVariable(filename="output/ants_TH.stoch.log", printgen=10)

# Initialize the MCMC object #
mymcmc = mcmc(mymodel, monitors, moves)

if (run_PPS == FALSE) {
  mymodel.graph("TH.dot")

  # Run Burnin and tuning
  mymcmc.burnin(generations=2000, tuningInterval=200)
  # Check acceptance rates
  mymcmc.operatorSummary()
  if (fromTrace == TRUE) {
    trace = readStochasticVariableTrace(file = "output/ants_TH_1.var")
    mymcmc.initializeFromTrace(trace)
  }
  mymcmc.operatorSummary()
  # Run the MCMC #
  mymcmc.run(generations=gens)
  # Check acceptance rates
  mymcmc.operatorSummary()
}

# Quit RevBayes #
#q()
